package;

import debug.FPSCounter;
import debug.GameLogDisplay;
import flixel.FlxGame;
import openfl.Lib;
import openfl.display.Sprite;
import openfl.events.Event;
import openfl.display.StageScaleMode;
import openfl.display.StageQuality;
import lime.app.Application;
import states.TitleState;
#if HSCRIPT_ALLOWED
import crowplexus.iris.Iris;
import psychlua.HScript.HScriptInfos;
#end
import mobile.backend.MobileScaleMode;
import openfl.events.KeyboardEvent;
import lime.system.System as LimeSystem;
import Sys;
import backend.Paths;
#if (linux || mac)
import lime.graphics.Image;
#end
#if COPYSTATE_ALLOWED
import states.CopyState;
#end
import backend.Highscore;
import flixel.tweens.FlxTween;
import flixel.tweens.FlxEase;
import backend.ClientPrefs;
import openfl.ui.Keyboard;

#if !mobile
import hxwindowmode.WindowColorMode;
#end

// NATIVE API STUFF, YOU CAN IGNORE THIS AND SCROLL //
#if (linux && !debug)
@:cppInclude('./external/gamemode_client.h')
@:cppFileCode('#define GAMEMODE_AUTO')
#end
// // // // // // // // //
class Main extends Sprite
{
	public static final game = {
		width: 1280, // WINDOW width
		height: 720, // WINDOW height
		initialState: TitleState, // initial game state
		framerate: 90, // default framerate
		skipSplash: true, // if the default flixel splash screen should be skipped
		startFullscreen: false // if the game should start at fullscreen mode
	};

	public static var fpsVar:FPSCounter;
	public static var gameLogVar:GameLogDisplay;

	public static final platform:String = #if mobile "Phones" #else "PCs" #end;

	// Background volume control variables
	private var backgroundVolumeTween:FlxTween;
	private var originalVolume:Float = 1.0;
	private var isInBackground:Bool = false;

	// You can pretty much ignore everything from here on - your code should go in your states.

	public static function main():Void
	{
		Lib.current.addChild(new Main());
		#if cpp
		cpp.NativeGc.enable(true);
		#elseif hl
		hl.Gc.enable(true);
		#end
	}

	public function new()
	{
		super();
		#if mobile
		#if android
		StorageUtil.requestPermissions();
		#end
		Sys.setCwd(StorageUtil.getStorageDirectory());
		#end
		backend.CrashHandler.init();

		#if (cpp && windows)
		backend.Native.fixScaling();
		#end

		#if VIDEOS_ALLOWED
		hxvlc.util.Handle.init(#if (hxvlc >= "1.8.0") ['--no-lua'] #end);
		#end

		#if LUA_ALLOWED
		Mods.pushGlobalMods();
		#end
		Mods.loadTopMod();

		FlxG.save.bind('funkin', CoolUtil.getSavePath());

		#if HSCRIPT_ALLOWED
		Iris.warn = function(x, ?pos:haxe.PosInfos)
		{
			Iris.logLevel(WARN, x, pos);
			var newPos:HScriptInfos = cast pos;
			if (newPos.showLine == null)
				newPos.showLine = true;
			var msgInfo:String = (newPos.funcName != null ? '(${newPos.funcName}) - ' : '') + '${newPos.fileName}:';
			#if LUA_ALLOWED
			if (newPos.isLua == true)
			{
				msgInfo += 'HScript:';
				newPos.showLine = false;
			}
			#end
			if (newPos.showLine == true)
			{
				msgInfo += '${newPos.lineNumber}:';
			}
			msgInfo += ' $x';
			if (PlayState.instance != null)
				PlayState.instance.addTextToDebug('WARNING: $msgInfo', FlxColor.YELLOW);
		}
		Iris.error = function(x, ?pos:haxe.PosInfos)
		{
			Iris.logLevel(ERROR, x, pos);
			var newPos:HScriptInfos = cast pos;
			if (newPos.showLine == null)
				newPos.showLine = true;
			var msgInfo:String = (newPos.funcName != null ? '(${newPos.funcName}) - ' : '') + '${newPos.fileName}:';
			#if LUA_ALLOWED
			if (newPos.isLua == true)
			{
				msgInfo += 'HScript:';
				newPos.showLine = false;
			}
			#end
			if (newPos.showLine == true)
			{
				msgInfo += '${newPos.lineNumber}:';
			}
			msgInfo += ' $x';
			if (PlayState.instance != null)
				PlayState.instance.addTextToDebug('ERROR: $msgInfo', FlxColor.RED);
		}
		Iris.fatal = function(x, ?pos:haxe.PosInfos)
		{
			Iris.logLevel(FATAL, x, pos);
			var newPos:HScriptInfos = cast pos;
			if (newPos.showLine == null)
				newPos.showLine = true;
			var msgInfo:String = (newPos.funcName != null ? '(${newPos.funcName}) - ' : '') + '${newPos.fileName}:';
			#if LUA_ALLOWED
			if (newPos.isLua == true)
			{
				msgInfo += 'HScript:';
				newPos.showLine = false;
			}
			#end
			if (newPos.showLine == true)
			{
				msgInfo += '${newPos.lineNumber}:';
			}
			msgInfo += ' $x';
			if (PlayState.instance != null)
				PlayState.instance.addTextToDebug('FATAL: $msgInfo', 0xFFBB0000);
		}
		#end

		#if LUA_ALLOWED Lua.set_callbacks_function(cpp.Callable.fromStaticFunction(psychlua.CallbackHandler.call)); #end
		Controls.instance = new Controls();
		ClientPrefs.loadDefaultKeys();
		#if ACHIEVEMENTS_ALLOWED Achievements.load(); #end
		#if mobile
		FlxG.signals.postGameStart.addOnce(() ->
		{
			FlxG.scaleMode = new MobileScaleMode();
		});
		#end
		// addChild(new FlxGame(game.width, game.height, #if COPYSTATE_ALLOWED !CopyState.checkExistingFiles() ? CopyState : #end game.initialState, game.framerate, game.framerate, game.skipSplash, game.startFullscreen));

		var game:FlxGame = new FlxGame(game.width, game.height, #if COPYSTATE_ALLOWED !CopyState.checkExistingFiles() ? CopyState : #end game.initialState,
			#if (flixel < "5.0.0") game.zoom, #end game.framerate, game.framerate, game.skipSplash, game.startFullscreen);
		// #if BASE_GAME_FILES
		// @:privateAccess
		// game._customSoundTray = backend.FunkinSoundTray;
		// #end
		addChild(game);

		fpsVar = new FPSCounter(10, 3, 0xFFFFFF);
		Lib.current.stage.addChild(fpsVar);
		Lib.current.stage.align = "tl";
		Lib.current.stage.scaleMode = StageScaleMode.SHOW_ALL;
		Lib.current.stage.quality = StageQuality.BEST;
		
		if (fpsVar != null)
		{
			fpsVar.visible = ClientPrefs.data.showFPS;
		}

		// 创建游戏日志显示
		gameLogVar = new GameLogDisplay();
		gameLogVar.setEnabled(ClientPrefs.data.enableGameLog);
		Lib.current.stage.addChild(gameLogVar);

		Language.load();

		// Sets the window to dark mode. (returns true if it was successful)
		#if !mobile
		WindowColorMode.setDarkMode();
		#end

		#if (linux || mac) // fix the app icon not showing up on the Linux Panel / Mac Dock
		var icon = Image.fromFile("icon.png");
		Lib.current.stage.window.setIcon(icon);
		#end

		#if html5
		FlxG.autoPause = false;
		FlxG.mouse.visible = false;
		#end

		FlxG.fixedTimestep = ClientPrefs.data.fixedTimestep;
		FlxG.game.focusLostFramerate = #if mobile 30 #else 60 #end;
		#if web
		FlxG.keys.preventDefaultKeys.push(TAB);
		#else
		FlxG.keys.preventDefaultKeys = [TAB];
		#end

		#if DISCORD_ALLOWED
		DiscordClient.prepare();
		#end

		#if desktop FlxG.stage.addEventListener(KeyboardEvent.KEY_UP, onKeyPress); #end

		#if mobile
		#if android FlxG.android.preventDefaultKeys = [BACK]; #end
		LimeSystem.allowScreenTimeout = ClientPrefs.data.screensaver;
		#end

		// Application.current.window.vsync = ClientPrefs.data.vsync;

		// shader coords fix
		FlxG.signals.gameResized.add(function(w, h)
		{
			if (fpsVar != null)
				fpsVar.positionFPS(10, 3, Math.min(w / FlxG.width, h / FlxG.height));
			if (FlxG.cameras != null)
			{
				for (cam in FlxG.cameras.list)
				{
					if (cam != null && cam.filters != null)
						resetSpriteCache(cam.flashSprite);
				}
			}

			if (FlxG.game != null)
				resetSpriteCache(FlxG.game);
		});

		// 监听 stage 窗口大小变化，更新 FPS 计数器位置
		Lib.current.stage.addEventListener(Event.RESIZE, function(e:Event):Void
		{
			if (fpsVar != null)
			{
				fpsVar.positionFPS(10, 3, 1);
			}
			if (gameLogVar != null)
			{
				gameLogVar.updatePositionOnResize();
			}
		});

		#if (desktop && !mobile)
		setCustomCursor();
		#end

		// 添加应用激活/停用事件监听
		Lib.current.stage.addEventListener(Event.DEACTIVATE, onAppDeactivate);
		Lib.current.stage.addEventListener(Event.ACTIVATE, onAppActivate);

		#if (cpp && windows && !mobile)
		// 延迟初始化窗口关闭回调，确保窗口完全创建后再设置
		haxe.Timer.delay(function()
		{
			backend.Native.setCloseCallback();
			// 启动关闭检查定时器
			startCloseCheckTimer();
		}, 100); // 延迟 100ms，更快启用关闭拦截
		#end
	}

	/**
	 * 渐隐关闭应用（仅Windows桌面端）
	 */
	#if (cpp && windows && !mobile)
	private static var isExiting:Bool = false;
	private static var closeCheckTimer:haxe.Timer = null;

	private static function checkCloseRequest():Void
	{
		if (backend.Native.isClosingRequested())
		{
			// 重置标志
			backend.Native.resetClosingRequested();

			// 停止定时器
			if (closeCheckTimer != null)
			{
				closeCheckTimer.stop();
				closeCheckTimer = null;
			}

			// 执行关闭流程
			startFadeOutAndExit();

			return; // 停止检查
		}
	}

	// 启动关闭请求检查（使用较慢的定时器减少性能影响）
	private static function startCloseCheckTimer():Void
	{
		if (closeCheckTimer != null)
			return; // 已经在运行

		closeCheckTimer = new haxe.Timer(100); // 每 100ms 检查一次（每秒 10 次）
		closeCheckTimer.run = checkCloseRequest;
	}

	// 停止关闭请求检查
	private static function stopCloseCheckTimer():Void
	{
		if (closeCheckTimer != null)
		{
			closeCheckTimer.stop();
			closeCheckTimer = null;
		}
	}

	private static function startFadeOutAndExit():Void
	{
		// 防止重复执行
		if (isExiting)
			return;
		isExiting = true;

		// 停止关闭检查定时器（性能优化）
		stopCloseCheckTimer();

		// 立即静音音乐（无延迟）
		if (FlxG.sound.music != null)
		{
			FlxG.sound.music.volume = 0;
		}

		// 立即播放关闭音效（无延迟）
		FlxG.sound.play(Paths.sound('BA/UI_BattleComplete'), 0.75, false);

		// 短暂延迟后开始渐隐（让音效开始播放）
		haxe.Timer.delay(function()
		{
			// 执行渐隐动画，持续500ms
			backend.Native.fadeOutWindow(500);

			// 延迟退出
			haxe.Timer.delay(function()
			{
				// 关闭 Discord（如果已初始化）
				#if DISCORD_ALLOWED
				if (DiscordClient.isInitialized)
					DiscordClient.shutdown();
				#end

				// 退出应用
				LimeSystem.exit(0);
			}, 550); // 550ms = 500ms 渐隐 + 50ms 缓冲
		}, 20); // 20ms 延迟，极快响应
	}
	#end

	// 应用进入后台时调用
	private function onAppDeactivate(e:Event):Void
	{
		if (isInBackground || !ClientPrefs.data.backgroundVolume)
			return;
		isInBackground = true;

		// 取消正在进行的恢复动画（如果存在）
		if (backgroundVolumeTween != null)
		{
			backgroundVolumeTween.cancel();
			backgroundVolumeTween = null;
		}

		// 保存当前音量
		originalVolume = FlxG.sound.volume;

		// 创建降低音量的动画
		backgroundVolumeTween = FlxTween.tween(FlxG.sound, {volume: ClientPrefs.data.backgroundVolumeLevel}, 1, {
			ease: FlxEase.quadOut,
			onComplete: function(twn:FlxTween)
			{
				backgroundVolumeTween = null;
			}
		});
	}

	// 应用回到前台时调用
	private function onAppActivate(e:Event):Void
	{
		if (!isInBackground || !ClientPrefs.data.backgroundVolume)
			return;
		isInBackground = false;

		// 取消正在进行的降低动画（如果存在）
		if (backgroundVolumeTween != null)
		{
			backgroundVolumeTween.cancel();
			backgroundVolumeTween = null;
		}

		// 创建恢复音量的动画
		backgroundVolumeTween = FlxTween.tween(FlxG.sound, {volume: originalVolume}, 0.5, {
			ease: FlxEase.quadOut,
			onComplete: function(twn:FlxTween)
			{
				backgroundVolumeTween = null;
			}
		});
	}

	static function resetSpriteCache(sprite:Sprite):Void
	{
		@:privateAccess {
			sprite.__cacheBitmap = null;
			sprite.__cacheBitmapData = null;
		}
	}

	function onKeyPress(event:KeyboardEvent)
	{
		if (Controls.instance.justReleased('fullscreen'))
		{
			FlxG.fullscreen = !FlxG.fullscreen;
			// 切换全屏后修复分辨率
			#if (cpp && windows)
			haxe.Timer.delay(function()
			{
				backend.Native.fixFullscreenResolution();
			}, 50);
			#end
		}

		// F3键切换日志显示
		if (event.keyCode == Keyboard.F3 && gameLogVar != null)
		{
			gameLogVar.toggleVisibility();
		}

		/*#if (cpp && windows && !mobile)
			// 拦截 ESC 键，执行渐隐关闭
			if (event.keyCode == Keyboard.ESCAPE)
			{
				// 阻止默认的 ESC 行为
				event.preventDefault();
				event.stopPropagation();
				event.stopImmediatePropagation();

				// 执行渐隐关闭
				startFadeOutAndExit();
			}
			#end */

		// F5键刷新当前state（使用CustomFadeTransition无缝切换）
		if (event.keyCode == Keyboard.F5 && FlxG.state != null)
		{
			// 获取当前state的类型
			var currentStateType:Class<flixel.FlxState> = Type.getClass(FlxG.state);

			// 创建新的state实例
			if (currentStateType != null)
			{
				var newState:flixel.FlxState = Type.createInstance(currentStateType, []);

				// 标记为刷新操作（CustomFadeTransition构造函数中会立即读取并重置）
				backend.CustomFadeTransition.isReloading = true;
				backend.CustomFadeTransition.reloadingStateType = currentStateType; // 保存要 reload 的 state 类型

				// 检查是否是MusicBeatState，使用自定义转场
				#if !macro
				// 尝试使用MusicBeatState的startTransition方法
				if (Std.isOfType(FlxG.state, backend.MusicBeatState))
				{
					backend.MusicBeatState.startTransition(newState);
				}
				else
				{
					// 非MusicBeatState，直接切换
					FlxG.switchState(newState);
				}
				#else
				FlxG.switchState(newState);
				#end
			}
		}
	}

	function setCustomCursor():Void
	{
		FlxG.mouse.load('assets/shared/images/cursor.png');
	}
}
